// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.3.2"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

ext{

    compileSdkVersion = 28
    buildToolsVersion = '28.0.1'


    group = 'org.eclipse.paho'

    sampleArchivesBaseName = 'org.eclipse.paho.android.sample'
    sampleVersion = '1.1.1'

    serviceArchivesBaseName = 'org.eclipse.paho.android.service'
    serviceVersion = '1.1.2'

    clientVersion = '1.1.0'

    mavenUrl = "https://repo.eclipse.org/content/repositories/paho-releases/"

    supportLibVersion = '28.0.0'

}

def getSdkDirFromLocalProperties() {
    def rootDir = project.rootDir
    def localProperties = new File(rootDir, "local.properties")
    Properties properties = new Properties()
    localProperties.withInputStream { instr ->
        properties.load(instr)
    }
    properties.getProperty('sdk.dir')
}


def androidHome = System.env['ANDROID_HOME']

def androidHomeIsDefined = androidHome?.trim()

def sdkDir = androidHomeIsDefined ? androidHome : getSdkDirFromLocalProperties()

println "SDK dir: $sdkDir"


allprojects {

    repositories {
        jcenter()
        maven {
            url "file://$sdkDir/extras/android/m2repository/"
        }
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }

    version = rootProject.ext.serviceVersion
    group = rootProject.ext.group
}
